\Auditory Oddball and 10 Slice Trigger or Volume Trigger MRI with Interleaved Photometry
\If using slice trigger MRI, TR and slices must be divisible by photometry frequency!
\MED and MRI use TTL- logic. Photometry, video, and inscopix use TTL+ logic.

\Timestamps: Oddballs = Y, Defaults = X, Inscopix Start+Stop = U, Photometry/Video = V

        \Timings       50ms------------------------------------------     med running
	\                  ----------__________----------__________    lasers
	\                   --------  --------  --------  --------     photometry, MRI, video
	\		              --------------------              tones
	\		    ---------------------------------------  inscopix
	\                   ---------------------------------------   med timestamps

\Created: 10/7/2019
\Last Updated: 9/13/2021

\Defined Constants
^FAN = 10
^SPECT = 17
^PURPLE = 18
^BLUE = 19
^MRI = 20
^INSCOPIX = 21
^VIDEO = 32

\Variables
\A = Photometry Frequency
\B = Dummy Scans
\C = Total MRI Repetitions
\D = MRI TR Counter
\E = MRI TR/Photometry Frequency
\F = MRI TR (ms)
\G = Photometry Offset
\H = MRI Trigger Type Variable
\I = MRI Slice Number Divider for Timing
\J = MRI Time per Slice
\K = Total Tones
\L = Oddball Tones
\M = Minimum Tones to Oddball
\N = Tone Interval
\O = Tone Duration
\P = Default Tone Frequency
\Q = Oddball Tone Frequency
\R = Tone Amplitude [db]
\S = Tone Rise Time
\T = Tone Interval/Photometry Frequency
\U = Timestamp Array: Inscopix
\V = Timestamp Array: Photometry
\W = Tone Selection List
\X = Timestamp Array: Default Tones
\Y = Timestamp Array: Oddball Tones
\Z(1) = X Array Position
\Z(2) = Y Array Position
\Z(3) = MRI trigger counter
\Z(4) = Photometry trigger counter
\Z(5) = MRI TR counter
\Z(6) = Tone trigger counter
\Z(7) = Current Tone
\Z(8) = Consecutive default tones counter
\Z(9) = U Array Position
\Z(10) = V Array Position
\Z(11) = Banked Oddball Tones

\Dimension Variables
DIM U = 10
DIM V = 100000
DIM X = 10000
DIM Y = 10000
DIM Z = 10

DISKCOLUMNS = 1

\List Variables
LIST W = 1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16,17,18,19,20,21,22,23,24,25,26,27,28,29,30,31,32,33,34,35,36,37,38,39,40,41,42,43,44,45,46,47,48,49,50

\Set Variables
VAR_ALIAS Spectrometer Freq [ms]  = A
VAR_ALIAS Spectrometer Interleave Gap [ms] = G
VAR_ALIAS Dummy Scans = B
VAR_ALIAS MRI Total Repetitions [MultipleofToneIntervalX50] = C
VAR_ALIAS MRI Repetition Time [ms] = F
VAR_ALIAS MRI Trigger [1volume2slice] = H
VAR_ALIAS MRI Slices [DoNOTChange] = I
VAR_ALIAS Oddball Tones [InEvery50Tones] = L
VAR_ALIAS Minimum Tones to Oddball = M
VAR_ALIAS Tone Interval [MultipleofSpectFreq] = N
VAR_ALIAS Tone Duration [ms] = O
VAR_ALIAS Default Tone Frequency [Hz] = P
VAR_ALIAS Oddball Tone Frequency [Hz] = Q
VAR_ALIAS Tone Amplitude [db] = R
VAR_ALIAS Tone Rise Time [ms] = S

\Z Pulses
\Z1 = Trigger at Frequency
\Z2 = End Session
\Z3 = MRI ON
\Z4 = Offset Trigger
\Z5 = Default Tone
\Z6 = Oddball Tone


S.S.1, \Setup Start and Finish
    S1,
	0.001": LOCKOFF ^PURPLE, ^BLUE, ^MRI, ^VIDEO, ^SPECT, ^INSCOPIX, ^FAN; ON ^PURPLE, ^BLUE, ^VIDEO, ^SPECT, ^INSCOPIX, ^FAN; SET N = 2000, A = 50, B = 0, L = 2, M = 10, O = 100, P = 4000, Q = 8000, R = 80, S = 10, Z(4) = 0, C = 1200, F = 1000, Z(1) = 0, Z(2) = 0, Z(3) = 0, Z(6) = 0, Z(8) = 0, H = 1, I = 10, G = 5, Z(9) = 0, Z(10) = 0, Z(11) = 0 ---> S2
    S2,
	#START: ~SetRack(MG, 1);~; \ The ANL-926 cards are in Rack 1
                     ~InitANL926;~; \ Reset ANL-926
                     ~SetAmp(MG, BOX, R);~; \ Initialize Amplitude
                     ~SetRF(MG, BOX, S);~; \ Initialize Rise\Fall Time
                    ~SetDur(MG, BOX, O);~; \ Initialize Duration
            SET E = F/A, T = N/A; RANDD Z(7) = W ---> S3
    S3,
        A#T: ADD Z(4); SET Z(5) = Z(4)/E; IF Z(5) <= C + B [@TRUE, @FALSE]
            @TRUE: Z1 --->SX
            @FALSE: Z2; ON ^BLUE, ^PURPLE, ^VIDEO, ^SPECT, ^INSCOPIX; OFF ^MRI, ^FAN; LOCKON ^BLUE, ^PURPLE, ^VIDEO, ^SPECT, ^INSCOPIX --->STOPABORTFLUSH

S.S.2, \Interleaving offset
    S1,
	#Z1: ---> S2
    S2,
	G#T: Z4 ---> S1

S.S.3, \Laser Interleaving 
    S1,
        #Z1: ON ^BLUE; OFF ^PURPLE ---> S2
        #Z2: ---> S3
    S2,
        #Z1: ON ^PURPLE; OFF ^BLUE --->S1
        #Z2: ---> S3
    S3,
        0.001":--->SX

S.S.4, \MRI Trigger
    S1,
        #START: IF H=1 [@TRUE, @FALSE]
            @TRUE: --->S2
            @FALSE: SET J=F/I --->S3
    S2,
        #Z4: ADD Z(3); IF Z(3) = E [@TRUE, @FALSE]
            @TRUE: SET Z(3) = 0; ON ^MRI; Z3 --->SX
            @FALSE: --->SX
        #Z2: ---> S4
    S3,
        #Z4: ADD Z(3); IF Z(3) * A = J [@TRUE, @FALSE]
            @TRUE: SET Z(3) = 0; ON ^MRI; Z3 --->SX
            @FALSE: --->SX
        #Z2: ---> S4
    S4,
        0.001":--->SX

S.S.5, \Spectrometer and Video Trigger
    S1,
        #Z4: OFF ^SPECT, ^VIDEO; SET V(Z(10)) = Z(5) - A/1000; ADD Z(10); SET V(Z(10)) = -987.987 ---> S2
    S2,
        0.002": ON ^SPECT, ^VIDEO --->S1

S.S.6, \MRI Trigger Off
    S1,
        #Z3: ---> S2
    S2,
        0.01": OFF ^MRI --->S1

S.S.7, \Oddball Paradigm
    S1,
        #Z4: IF Z(5) <= B [@TRUE, @FALSE]
            @TRUE: ---> SX
            @FALSE: ADD Z(6); IF Z(6) >= T [@TRUE, @FALSE]
                @TRUE: SET Z(6) = 0; RANDD Z(7) = W; IF Z(7) <= L [@TRUE, @FALSE]
			@TRUE: IF Z(8) >= M [@TRUE, @FALSE]
				@TRUE: SET Z(8) = 0; Z6 ---> SX
				@FALSE: ADD Z(8); ADD Z(11); Z5 ---> SX
			@FALSE: IF (Z(8) >= M) AND (Z(11) >= 1) [@TRUE, @FALSE]
				@TRUE: SET Z(8) = 0; SUB Z(11); Z6 ---> SX
				@FALSE: ADD Z(8); Z5 ---> SX
		@FALSE: ---> SX

S.S.8, \Default Tone
    S1,
        #Z5: ~OnFreq(MG, BOX, P);~ --->SX

S.S.9, \Oddball Tone
    S1,
        #Z6: ~OnFreq(MG, BOX, Q);~ --->SX

S.S.10, \Inscopix Trigger
    S1,
	#Z4: OFF ^INSCOPIX; SET U(Z(9)) = Z(5) - A/1000; ADD Z(9); SET U(Z(9)) = -987.987--->S2
    S2,
	#Z2: ON ^INSCOPIX; SET U(Z(9)) = Z(5) - A/1000; ADD Z(9); SET U(Z(9)) = -987.987--->SX

S.S.11, \Timestamps and Display
    S1,
        #Z5: SET X(Z(1)) = Z(5) - A/1000; ADD Z(1); SET X(Z(1)) = -987.987; SHOW 1, Default Tones, Z(1) ---> SX
        #Z6: SET Y(Z(2)) = Z(5) - A/1000; ADD Z(2); SET X(Z(2)) = -987.987; SHOW 2, Oddball Tones, Z(2) ---> SX
        #Z1: SHOW 3, Time Remaining, C+B-Z(5) ---> SX
