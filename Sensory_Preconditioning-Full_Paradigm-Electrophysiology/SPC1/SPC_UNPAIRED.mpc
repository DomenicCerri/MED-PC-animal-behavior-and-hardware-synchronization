\ Sensory Pre-Conditioning (SPC): UNPAIRED PreConditioning training (multi-cue)
\ Description: 12 each of NOISE(S2), Flashing(R)LIGHT(S1), TONE(S2), and Solid(L)Light(S1)
\ Created: 11/19/2012
\ Last Updated:
\ Author: Dom Cerri

\Inputs
^LLever=2
^RLever=1
^Foodcup=5

\Outputs
^RPanelLight=3
^LPanelLight=4
^Tone=6
^Noise=7
^Houselight=8
^Fan=10
^Cue_LED=12
^TTL_S2TONE=17
^TTL_S1SolidLight=18
^TTL_S2NOISE=19
^TTL_S1FlashingLight=20
^TTL_FOODCUP_ENTRY=21
^TTL_FOODCUP_EXIT=22

\Variables
\ A = LIST ITIs
\ B = LIST TRIAL TYPE
\ C = Current ITI
\ D = Current Trial Type
\ E = ITI Position
\ F = Trial Type Position
\ G = 
\ H = 
\ I = 
\ J = 
\ K = 
\ L = 
\ M = 
\ N = 
\ O = ToneGenerator - Tone Frequency
\ P = ToneGenerator - Tone Volume (amplitude)
\ Q = ToneGenerator - Attack/Decay Time
\ R = ToneGenerator - Duration of Tone
\ S = Session Timer
\ T = 
\ U = 
\ V = 
\ W = 
\ X = 
\ Y = Temp Counter for FC entries/exits
\ Z = 


LIST A = 50", 130", 85", 115", 100", 125", 100", 90", 90", 85", 95", 75", 90", 40", 65", 45", 105", 115", 40", 70", 95", 115", 55", 40", 100", 70", 45", 85", 50", 45", 120", 80", 85", 65", 125", 40", 120", 70", 80", 75", 95", 95", 55", 120", 105", 130", 65", 55"
LIST B = 1,2,1,3,4,4,3,2,2,3,1,4,2,3,1,1,4,2,3,4,2,3,3,1,4,2,3,4,4,1,2,4,2,1,3,1,4,3,2,1,1,3,3,4,2,4,2,1

DISKCOLUMNS = 1

S.S.1,
      S1,
          .1": ON ^LLever, ^RLever; LOCKON ^LLever, ^RLever ---> S2
      S2,
          #START: ON ^Fan, ^Houselight ---> S3
      S3, 
          .1": IF M < 49 [@TRUE, @FALSE]
               @TRUE: SHOW 5, TOTAL, M ---> S4
               @FALSE: OFF ^Houselight, ^Fan ---> STOPABORTFLUSH
      S4, 
          .1": LIST C = A(E); LIST D = B(F) ---> S5
      S5,
          C#T: ON ^Cue_LED ---> S6
      S6, 
          10": OFF ^Cue_LED; Z(D); ADD M ---> S7
      S7,
          #Z5: ---> S3

S.S.2,
	S1,
		#Z1: ON ^NOISE, ^TTL_S2NOISE ---> S2
	S2,
		.001": OFF ^TTL_S2NOISE ---> S3
	S3,
		9.999": OFF ^NOISE; Z5 ---> S1	
S.S.3,
	S1,

		#Z2: ---> S3
	S2, 
		0.25": ON ^RPanelLight ---> S3
		#z5: OFF ^RPanelLight ---> S1
	S3, 
		0.25": OFF ^RPanelLight ---> S2
		#z5: OFF ^RPanelLight ---> S1
S.S.4,
	S1,
		#Z2: ON ^RPanelLight, ^TTL_S1FlashingLight ---> S2
	S2,
		.001": OFF ^TTL_S1FlashingLight ---> S3
	S3,
		9.999": Z5 ---> S1
S.S.5,
	S1,
		#Z3: ~OnFreq(MG, BOX,O);~; ---> Sx
S.S.6,
	S1,
        #Start: SET O = 800;           \ default freq = 1000 hz 
                SET P = 90;             \ volume = 95 db
                SET Q = 10;             \ rise\fall time = 10 ms
                SET R = 10000;           \ duration = 10000 ms
                ~InitANL926;~;        \ reset ANL-926
                ~SetFreq(MG, BOX,O);~;      \ initialize frequency
                ~SetAmp(MG, BOX,P);~;       \ initialize amplitude
                ~SetRF(MG, BOX,Q);~;        \ initialize rise\fall time
                ~SetDur(MG, BOX,R);~;       \ initialize duration
                ~SETCLICKFREQ(MG,BOX,10);~;--->SX         \ SET CLICK FREQUENCY 
S.S.7,
	S1,
		#Z3: ON ^TTL_S2TONE ---> S2
	S2,
		.001": OFF ^TTL_S2TONE ---> S3
	S3,
		9.999": Z5 ---> S1
S.S.8,
	S1,
		#Z4: ON ^LPanelLight, ^TTL_S1SolidLight ---> S2
	S2,
		.001": OFF ^TTL_S1SolidLight ---> S3
	S3,
		9.999": OFF ^LPanelLight; Z5 ---> S1
S.S.9,
	S1,
		#START: ---> S2
	S2,
		#R^Foodcup: ADD Y; if Y >= 2 [@BeamBreak, @NoBeamBreak]
			@BeamBreak: Z6; Set Y = 0 ---> S3
			@NoBeamBreak: ---> SX
	S3,
	        0.02": Z7 ---> S2
        	#R^Foodcup: ---> S3
S.S.10, 
	S1,
		#Z6: ON 21 ---> S2
	S2,
		0.001": OFF 21 ---> S1
S.S.11, 
	S1,
		#Z7: ON 22 ---> S2
	S2,
		0.001": OFF 22 ---> S1
S.S.12, 
       S1,
	  #START: ---> S2
       S2, 
	  .1": Set S = S + .1"; SHOW 5, Min, S/1' ---> SX


	
