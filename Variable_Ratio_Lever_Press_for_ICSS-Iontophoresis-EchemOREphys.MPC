\ICS VTO short inter trial intervals (5-25s) with Ionto *0.200" TTLs

\Last Edit: 6/19/2013 by DHC

\DEFAULTS *can change variables in med once program is loaded (times are in ms)
\PRESSES_TO_NR = 5 *Set negative to disable
\TONE_TO_LEVER_TIME = 0"
\PRESS_TO_STIM_TIME = 0"
\MAX_TRIALS = 200
\IONTO_DURATION = 15000ms
\IONTO_TO_CUE_TIME = 1000ms

\DEFINE CONSTANTS
^RLEVER=2
^RCUE=3
^TONE=6
^WHITE NOISE=7
^HOUSE LIGHT=8
^STIM=9
^FAN=10
^IONTO=31 /Inverse

^TTL LEVER PRESS=17 	\(Plexon Event 3) (Echem 1)
^TTL STIM=18 		\(Plexon Event 4) (Echem 2)
^TTL LEVER OUT=19 	\(Plexon Event 5) (Echem 3)
^TTL TONE=20 		\(Plexon Event 6) (Echem 4)
^TTL NOSTIM=21		\(Plexon Event 7) (Echem 5)
^TTL TIMEOUT=22 	\(Plexon Event 8) (Echem 6)
^TTL IONTO ON=23	\(Plexon Event 9) (Echem 7)
^TTL IONTO OFF=24	\(Plexon Event 10) (Echem 8)

\A=LEVER PRESSES
\B=UNREINFORCED TRIAL
\C=TOTAL TRIALS
\D=STIMS
\E=TRIALS TO END SESSION
\F=PRESSES TO NO STIM
\G=TIME BETWEEN TONE ON AND LEVER EXTENSION
\H=CURRENT PRESSES
\I=NO STIMS
\J=IONTO DURATION
\K=TIME BETWEEN IONTO OFF AND CUE ON
\S=TIME BETWEEN LEVER PRESS AND STIMULATION

LIST X= 5", 6", 7", 8", 9", 10", 11", 12", 13", 14", 15", 16", 17", 18", 19", 20", 21", 22", 23", 24", 25"


VAR_ALIAS PRESSES_TO_NR = F
VAR_ALIAS TONE_TO_LEVER_TIME = G
VAR_ALIAS PRESS_TO_STIM_TIME = S
VAR_ALIAS MAX_TRIALS = E
VAR_ALIAS IONTO_DURATION = J
VAR_ALIAS IONTO_TO_CUE_TIME = K

S.S.1, 
S1,
1": LOCKOFF 2; ON 1,2,10;SET A=0; SET B=0; SET D=0; SET E=200; SET F=5; SET G=0"; SET H=0; SET J=15"; SET K=1"; SET S=0"; ON 31--->S2
S2,
#START: ON 3,7;--->S3
S3,
30":--->S4
S4,
.001":RANDI Y=X;--->S5
S5,
Y#T:OFF 31; ON 23; Z23--->S6
S6,
J#T:ON 31,24; Z24--->S7
S7,
K#T:ON 8,6; OFF 3; ON 20; Z20--->S8
S8,
G#T: OFF 2;ON 19; Z19--->S9
S9,
#R2:ON 2,3,17; OFF 8,6; Z17; ADD A; ADD H;--->S11 
15":ON 2,3,22; OFF 8,6; Z22; ADD B;--->S10
S10,
0.001": ADD C;--->S4
S11,
S#T: IF H<>F [@ICSS, @NR]
@ICSS: ON 9,18; Z18; ADD D;--->S10
@NR: ON 21; Z21; ADD I; SET H=0;--->S10

S.S.2,
S1,
#START:--->S2
S2,
1":SHOW 1, PRESSES, A; SHOW 2, TIMEOUTS, B; SHOW 3, STIMS, D; SHOW 4, NOSTIMS, I;--->S3
S3,
1":SHOW 5, TRIALS, C;--->S2

S.S.3,
S1,
#START:--->S2
S2,
5": IF C=E [@TRUE,@FALSE]
         @TRUE:OFF 3,6,7,8,9,10; ON 2,31; LOCKON 2;--->STOPABORTFLUSH 
         @FALSE:--->SX

S.S.4,
S1,
#START:--->S2
S2,
#Z20:--->S3
S3,
.2":OFF 20;--->S2

S.S.5,
S1,
#START:--->S2
S2,
#Z19:--->S3
S3,
.2":OFF 19;--->S2

S.S.6,
S1,
#START:--->S2
S2,
#Z17:--->S3
S3,
.2":OFF 17;--->S2

S.S.7,
S1,
#START:--->S2
S2,
#Z22:--->S3
S3,
.2":OFF 22;--->S2

S.S.8,
S1,
#START:--->S2
S2,
#Z18:--->S3
S3,
.2":OFF 18,9;--->S2

S.S.9,
S1,
#START:--->S2
S2,
#Z21:--->S3
S3,
.2":OFF 21;--->S2

S.S.10,
S1,
#START:--->S2
S2,
#Z23:--->S3
S3,
.2":OFF 23;--->S2

S.S.11,
S1,
#START:--->S2
S2,
#Z24:--->S3
S3,
.2":OFF 24;--->S2